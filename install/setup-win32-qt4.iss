; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
#ifndef AppName
  #define AppName "Traquera"
#endif

;#ifndef AppVer
;#define AppVer "3.3"
;#endif

#ifndef Publisher
 #define Publisher ("Sergey Shmakov")
#endif

#ifndef OutputBaseFilename
  #define OutputBaseFilename (AppName + "_setup")
#endif

#ifndef BuildPath
;//#error BuildPath must be defined
  #define BuildPath "C:\gits\build\traquera-4_8_7-Release"
#endif

#ifndef QTDIR
  #define QTDIR = GetEnv("QTDIR")
#endif

#if QTDIR == ""
 #define QTDIR "C:\Qt\4.8.4"
#endif

#ifndef VARIANT
  #define VARIANT "FULL"
#endif    

#if VARIANT == "RS" || VARIANT == "FULLRS" 
  #define PLUGIN_TRK
  #define PLUGIN_TRK_RS
  #define PLUGIN_MS
  #define PLUGIN_MS_RS
  #define CLIENT_RS
#elif VARIANT == "JIRA"
  #define PLUGIN_JIRA
#else
  #define PLUGIN_TRK
  #define PLUGIN_JIRA
  #define PLUGIN_MS
#endif

#ifdef ProtectFile
  #define ApplicationExe (BuildPath + "\client\release\traquera-protect.exe")
#else
  #define ApplicationExe (BuildPath + "\client\release\traquera.exe")
#endif

#define AppVer GetStringFileInfo(ApplicationExe,PRODUCT_VERSION)

#pragma warning "BuildPath = " + BuildPath
#pragma warning "AppVer = " + AppVer


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{A55FF037-34CA-4879-A4C3-14D36230A800}
AppName={#AppName}
AppVersion={#AppVer}
;AppVerName=TrackTasks 2.0
AppVerName={#AppName} {#AppVer}
AppPublisher={#Publisher}
DefaultDirName={pf}\{#AppName}
DefaultGroupName={#AppName}
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename={#OutputBaseFilename}
Compression=lzma
SolidCompression=yes
LicenseFile=license.txt

[Components]
Name: "main"; Description: "{cm:NameAndVersion,{#AppName},{#AppVer}}"; Types: full custom compact; Flags: fixed
Name: "plugins"; Description: "{cm:Plugins}"; Types: full custom
#ifdef PLUGIN_JIRA
Name: "plugins/jira"; Description: "{cm:PluginFor,Atlassian JIRA}"; Types: full custom
#endif
#ifdef PLUGIN_TRK
Name: "plugins/tracker"; Description: "{cm:PluginFor,PVCS Tracker 7.x}"; Types: full custom
#endif
#ifdef PLUGIN_MS
Name: "plugins/msplans"; Description: "{cm:PluginFor,Microsoft Project}"; Types: full custom
#endif

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: {#ApplicationExe}; DestName: "traquera.exe"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#BuildPath}\client\*.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "redistribute\*"; DestDir: "{app}"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#QTDIR}\bin\phonon4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\Qt3Support4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtCLucene4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtCore4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtDeclarative4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtDesigner4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtDesignerComponents4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtGui4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtHelp4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtMultimedia4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtNetwork4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtOpenGL4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtScript4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtScriptTools4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtSql4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtSvg4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtTest4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtWebKit4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtXml4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\bin\QtXmlPatterns4.dll"; DestDir: "{app}"; Components: main; Flags: ignoreversion
Source: "{#QTDIR}\plugins\imageformats\*"; DestDir: "{app}\imageformats"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#QTDIR}\plugins\sqldrivers\*"; DestDir: "{app}\sqldrivers"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\client\data\*"; DestDir: "{app}\data"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs 
Source: "..\client\lang\*"; DestDir: "{app}\lang"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs 
Source: "license.txt"; DestDir: "{app}"; Components: main;

#ifdef PLUGIN_JIRA
; Jira
Source: "{#BuildPath}\client\plugins\jira\jira.dll"; DestDir: "{app}\plugins\jira"; Components: "plugins/jira"
Source: "{#BuildPath}\client\plugins\jira\data\*"; DestDir: "{app}\plugins\jira\data"; Components: "plugins/jira"
Source: "{#BuildPath}\client\plugins\jira\lang\*"; DestDir: "{app}\plugins\jira\lang"; Components: "plugins/jira"
#endif

#ifdef PLUGIN_TRK
; Tracker
Source: "{#BuildPath}\client\plugins\tracker\tracker.dll"; DestDir: "{app}\plugins\tracker"; Components: "plugins/tracker"
Source: "{#BuildPath}\client\plugins\tracker\plugin.ini"; DestDir: "{app}\plugins\tracker"; Components: "plugins/tracker"
Source: "{#BuildPath}\client\plugins\tracker\data\*"; DestDir: "{app}\plugins\tracker\data"; Components: "plugins/tracker"
Source: "{#BuildPath}\client\plugins\tracker\lang\*"; DestDir: "{app}\plugins\tracker\lang"; Components: "plugins/tracker"
#endif

#ifdef PLUGIN_MS
; MSProject
Source: "{#BuildPath}\client\plugins\msplans\msplans.dll"; DestDir: "{app}\plugins\msplans"; Components: "plugins/msplans"
Source: "{#BuildPath}\client\plugins\msplans\data\*"; DestDir: "{app}\plugins\msplans\data"; Components: "plugins/msplans"
#endif

#ifdef CLIENT_RS
Source: "RS\client\*"; DestDir: "{app}\data"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs 
#endif
#ifdef PLUGIN_TRK_RS
Source: "RS\tracker\*"; DestDir: "{app}\plugins\tracker\data"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs 
#endif
#ifdef PLUGIN_MS_RS
Source: "RS\msplans\*"; DestDir: "{app}\plugins\msplans\data"; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs 
#endif

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#AppName}"; Filename: "{app}\traquera.exe"
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#AppName}"; Filename: "{app}\traquera.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#AppName}"; Filename: "{app}\traquera.exe"; Tasks: quicklaunchicon

[Registry]
Root: HKCU; Subkey: "{code:GetRegPath}"; Flags: uninsdeletekeyifempty
Root: HKCU; Subkey: "{code:GetRegPath}"; ValueType: string; ValueName: "UserDBType"; ValueData: "QSQLITE"; Flags: createvalueifdoesntexist
Root: HKCU; Subkey: "{code:GetRegPath}"; ValueType: string; ValueName: "UserDBPath"; ValueData: "{userappdata}\{#AppName}\user.db"; Flags: createvalueifdoesntexist
Root: HKCU; Subkey: "{code:GetRegPath}"; ValueType: string; ValueName: "UserDBUser"; ValueData: ""; Flags: createvalueifdoesntexist
Root: HKCU; Subkey: "{code:GetRegPath}"; ValueType: string; ValueName: "UserDBPassword"; ValueData: ""; Flags: createvalueifdoesntexist

[Ini]
#ifdef PLUGIN_TRK
Filename: "{app}\plugins\tracker\plugin.ini"; Section: "Init"; Key: "LibPath"; String: "{code:GetTrackerPath}"; Components: "plugins/tracker"
#endif

[Run]
Filename: "{app}\traquera.exe"; Description: "{cm:LaunchProgram,Traquera}"; Flags: nowait postinstall skipifsilent

[Languages]
Name: "ru"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "en"; MessagesFile: "compiler:Default.isl"

[CustomMessages]
ru.SelectTrkFolder=Укажите папку PVCS Tracker
ru.Plugins=Расширения
ru.PluginFor=Расширение для %1

en.SelectTrkFolder=Select folder with PVCS Tracker
en.Plugins=Plugins
en.PluginFor=Plugin for %1

[Code]
function cm(Message: String): String;
begin
  Result := ExpandConstant('{cm:' + Message + '}');
end;


function GetRegPath(Param: String): String;
begin
  Result := ExpandConstant('Software\R-Style Softlab\TrackTasks');
end;


function GetTrackerPath(Param: String): String;
var
  Path : String;
  Res: String;
begin
  Res := ExpandConstant('{pf32}\PVCS\Tracker\nt');
  Path := FileSearch('trktooln.dll', Res);
  if Path = '' then
  begin
    Path := Res;
    if BrowseForFolder(cm('SelectTrkFolder'), Path, false) then
      Res :=  Path;
  end;
  StringChangeEx(Res, '\', '/', True);  
  Result := Res;
end;

(*
var
  Page: TInputDirWizardPage;
  DataDir: String;


procedure InitializeWizard();
begin
...

// Create the page
Page := CreateInputDirPage(wpWelcome,
  'Select Personal Data Location', 'Where should personal data files be stored?',
  'Personal data files will be stored in the following folder.'#13#10#13#10 +
  'To continue, click Next. If you would like to select a different folder, click Browse.',
  False, 'New Folder');

// Add item (with an empty caption)
Page.Add('');

// Set initial value (optional)
Page.Values[0] := ExpandConstant('{userappdata}\My Company\My Program');

...

// Read value into variable
DataDir := Page.Values[0];
  
end;
*)
